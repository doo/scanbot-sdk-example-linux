plugins {
    id 'java'
    id 'application'
}

group = 'io.scanbot.sdk'
version = '1.0'

repositories {
    mavenCentral()
}

def SCANBOTSDK_VERSION = "<SCANBOTSDK_VERSION>"
def SCANBOTSDK_ARCH   = "<SCANBOTSDK_ARCH>" // aarch64 | x86_64

def scanbotJarName = "scanbotsdk-${SCANBOTSDK_VERSION}-linux-${SCANBOTSDK_ARCH}.jar"
def scanbotJarFile = file("libs/${scanbotJarName}")

tasks.register("verifyScanbotJar") {
    doLast {
        if (!scanbotJarFile.exists()) {
            throw new org.gradle.api.GradleException(
                "${scanbotJarFile.path} not found. Please place this file into the libs/ folder."
            )
        } else {
            println "Использую JAR: ${scanbotJarFile.name}"
        }
    }
}

tasks.withType(JavaCompile).configureEach { dependsOn tasks.named("verifyScanbotJar") }
tasks.named("test") { it.dependsOn tasks.named("verifyScanbotJar") }
tasks.named("run")  { it.dependsOn tasks.named("verifyScanbotJar") }
tasks.named("jar")  { it.dependsOn tasks.named("verifyScanbotJar") }

dependencies {
    implementation files(scanbotJarFile)

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

application {
    mainClass = "io.scanbot.sdk.ScanbotSDKExample"
}

test {
    useJUnitPlatform()
}