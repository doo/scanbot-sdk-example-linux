plugins {
    id 'java'
    id 'application'
}

// TODO Add your SCANBOTSDK_VERSION here.
def SCANBOTSDK_VERSION = "<SCANBOTSDK_VERSION>"

def arch = System.getProperty("os.arch")
def SCANBOTSDK_ARCHITECTURE = (arch.contains("arm") || arch.contains("aarch64")) ? "aarch64" : "x86_64"

group = 'io.scanbot.sdk'
version = '1.0'

repositories {
    mavenCentral()
}

def urlFile = { url, name ->
    File file = new File("${layout.buildDirectory.get()}/libs/${name}")
    file.parentFile.mkdirs()
    if (!file.exists()) {
        new URL(url).withInputStream { downloadStream ->
            file.withOutputStream { fileOut ->
                fileOut << downloadStream
            }
        }
    }
    files(file.absolutePath)
}

dependencies {
    implementation urlFile("https://github.com/doo/scanbot-sdk-example-linux/releases/download/standalone-sdk%2Fv${SCANBOTSDK_VERSION}/scanbotsdk-${SCANBOTSDK_VERSION}-linux-${SCANBOTSDK_ARCHITECTURE}.jar", "scanbotsdk.jar")
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    files()
}

application {
    mainClassName = "io.scanbot.sdk.ScanbotSDKExample"
}

test {
    useJUnitPlatform()
}